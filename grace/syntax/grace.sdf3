module grace

imports

  general
  keywordRejections
  grace-lowered

context-free start-symbols

  Program
  Exp

context-free syntax

  Program.Program               = <<{Statement "\n"}*> <EOF>>

context-free syntax

  Statement.Dialect             = <dialect <STRING>>
  Statement.Import              = <import <STRING> as <Identifier><Annotations>>
//  Statement.Assignment          = <<Assignment>;>
  Statement.Declaration         = <<Declaration>;>
  Statement.Expression          = <<Exp>;>

context-free syntax

//  Assignment.Assignment         = <<ID> := <Exp>>
  VarInit.VarInit               = < := <Exp>>
  VarInit.NoVarInit             = <>
  Declaration.Constant          = <def <Identifier><TypeAnn><Annotations>=<Exp>>
  Declaration.Variable          = <var <Identifier><TypeAnn><Annotations><VarInit>>
  Declaration.MethodDecl        = <method <MethodNames><Annotations><TypeRuleRightHand> <MethodBody>>
  Declaration.ClassDecl         = <
                                   class <ClassName> <TypeRuleRightHand> {
                                       <Inherit><Use*><{Statement "\n"}*>
                                   }
                                  >
  Declaration.TraitDecl         = <trait <MethodNames> <MethodBody>>
  Use.Use                       = <
                                       use <Exp> <Modifier*>;

                                  >
  
  ClassName.FullStop            = <<ID>.<MethodNames>>
  ClassName.MethodName          = <<MethodNames>>
                                  
  MethodNames.Single            = <<MethodNameNoParam>>
  MethodNames.Multiple          = <<MethodName+>>
                                  
  MethodNameNoParam.MethodID    = <<Identifier><TypeArg>>
  MethodNameNoParam.MethodOp    = <<OperatorCF><TypeArg>>
  
  MethodName.MethodID           = <<Identifier><TypeArg> <Params>>
  MethodName.MethodOp           = <<OperatorCF><TypeArg> <Params>>

  Declaration.TypeDecl          = <type <Identifier><TypeArg> <Annotations> = <TypeDeclBody>>
  
  TypeDeclBody.TypeDeclBlock    = <<TypeBlock>>
  TypeDeclBody.TypeDeclExp      = <<TypeExp>>
  
  TypeBlock.TypeBlock           = <{<{TypeRule "\n"}*>}>
  
  TypeRule.TypeRule             = <<MethodNames> <TypeRuleRightHand>;>
  TypeRuleRightHand.RH          = [ -> [TypeExp]]
  TypeRuleRightHand.NoRH        = <>
  
//  TypeConf.TypeConf             = [[TypeExp] <: [TypeExp]]
  TypeExp.Union                 = <<TypeExp> + <TypeExp>> {left}
  TypeExp.Subtract              = <<TypeExp> - <TypeExp>> {left}
  TypeExp.Intersect             = <<TypeExp> & <TypeExp>> {left}
  TypeExp.Variant               = <<TypeExp> | <TypeExp>> {left}
  TypeExp.TypeID                = <<Identifier><TypeArg>>
  
  TypeExp.AnonType              = <type <TypeBlock>>
  TypeExp.Interface             = <interface <TypeBlock>>
  
  TypeArg.TypeArg               = <[[<{Identifier ","}*>]]>
  TypeArg.NoTypeArg             = <>
  
  TypeAnn.TypeAnn               = <: <TypeExp>>
  TypeAnn.NoTypeAnn             = <>
  
  Identifier.ID                 = <<ID>>
  Identifier.WildCard           = <_>
  
  Annotations.Annotations       = < is <{Annotation ", "}+>>
  Annotations.NoAnnotations     = <>
  
  Annotation.Public             = <public>
  Annotation.Readable           = <readable>
  Annotation.Writeable          = <writable>
  Annotation.Confidential       = <confidential>
  Annotation.Manifest           = <manifest>
  Annotation.Overrides          = <override>
  
  Params.Params                 = <(<{Param ", "}+>)>
//  Param.Param                   = <<Identifier>>
  Param.ParamWType              = <<Identifier><TypeAnn>>

  MethodBody.MethodBody         = <
                                   {
                                       <{Statement "\n"}*>
                                   }
                                  >
   
context-free syntax
  
  Exp.ObjectDecl                = <
                                   object {
                                       <Inherit><Use*><{Statement "\n"}*>
                                   }
                                  >
  
  Exp.TypeExp                   = <<TypeExp>> {avoid}
  Exp.Number                    = <<NUM>>
  Exp.String                    = <<STRING>> {prefer}
  Exp.InterpolatedString        = <<STRINGINTSTART><Interpolated*><InterpolatedEnd>>
  Interpolated.IntPol           = <<Exp> <STRINGINTMID>>
  InterpolatedEnd.IntPolEnd     = <<Exp> <STRINGINTEND>>
  Exp.Boolean                   = <<Boolean>>

  
  Exp                           = <(<Exp>)>                    {bracket}
  
  Exp.MCallOpEx                 = <<Exp> <OperatorCF> <Exp>>   {left}
  Exp.MCallOpExAssign           = <<Exp> := <Exp>>             {left} // prefer
  Exp.MCallWDot                 = <<Exp>.<Part+>>              {left}
  Exp.MCallImpl                 = <<Part+>>                    {left}
  
  Exp.Self                      = <self>
  Exp.Outer                     = <outer>
  Exp.MQCallOuter               = <<Exp>.outer>                {left}
  Exp.MQCallSelf                = <<Exp>.self>                 {left}      
  
//  Exp.MCallPrefixOp             = <<OperatorCF> <CallArgs>>    {left}
  Exp.MCallPrefixOpExp          = <<OperatorCF> <Exp>>         {right}
  
  Exp.Ellipsis                  = <...>
  Exp.LineupExp                 = <<Lineup>>
  Exp.BlockExp                  = <<Block>>
  
  Exp.Return                    = <return <Exp>>               {right}
  
  Exp.MatchCase                 = <
                                   match (<Exp>)
                                       <{Case "\n"}+>
                                  > {prefer} // over methodcall
  Case.Case                     = [case {[CaseExp] [Arrow] [{Statement "\n"}*]}]
  Case.CaseParen                = [case ( {[CaseExp] [Arrow] [{Statement "\n"}*]} )]
  CaseExp.CaseExp               = <<CaseLiteral>>
  CaseExp.CaseExpMulti          = <<CaseLiteral> <BoolOp> <CaseLiteral>>
  CaseExp.ExpParens             = <(<Exp>)>
  CaseExp.CIdentifier           = <<Identifier>>
  CaseExp.ExpTyped              = <<Identifier> : <TypeExp>>
  BoolOp.Or                     = <|>
  BoolOp.And                    = <&>
//  CaseType.Type                 = <<Identifier>>
  CaseLiteral.CNumber           = <<NUM>>
  CaseLiteral.CString           = <<STRING>>
  CaseLiteral.CBoolean          = <<Boolean>>
  
  Arrow.Ascii                   = [->]
  
  
context-free syntax
  
  Part.Part                     = <<Identifier><CallArgs>>
  
  CallArgs.ArgsParen            = < (<{Exp ", "}+>)>           {left}
  CallArgs.NoArgs               = <>

  CallArgs.ArgBlock             = < <Block>>
  CallArgs.ArgNumber            = < <NUM>>
  CallArgs.ArgString            = < <STRING>>
  CallArgs.ArgLineup            = < <Lineup>>
  CallArgs.ArgBoolean           = < <Boolean>>
    
  OperatorCF.OperatorCF         = <<Operator>>
  
  Inherit.Inherit               = <
                                   inherit <Exp> <Modifier*>;
                  
                                  >
  Inherit.NoInherit             = <>
                                  
  Modifier.AliasModifier        = <<Alias>>
  Modifier.ExcludeModifier      = <<Exclude>>
  Alias.Alias                   = < alias <MethodNames> = <MethodNames>>
  Exclude.Exclude               = < exclude <MethodNames>>
  
  Boolean.True                  = <true>
  Boolean.False                 = <false>
  Lineup.Lineup                 = <[<{Exp ", "}*>]>
  Block.Block                   = <{<{Statement "\n"}*>}>
  Block.BlockWParams            = <{<BlockParams> <{Statement "\n"}*>}> {prefer}
  BlockParams.BlockParams       = [[{Param ", "}*] ->]
  
context-free priorities

  Exp.MCallWDot > Exp.Return
  ,
  Exp.MCallWDot >
  Exp.MCallPrefixOpExp >
  Exp.MCallOpEx >
  Exp.MCallOpExAssign >
  Exp.TypeExp
  ,
  Exp.MQCallOuter >
  Exp.MCallOpExAssign
  ,
  Exp.MQCallSelf >
  Exp.MCallOpExAssign
  
template options

  tokenize : "."
  tokenize : "("
  tokenize : ")"
  tokenize : "{"
  tokenize : "}"
