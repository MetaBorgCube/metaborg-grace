module trans/semantics/booleans

imports
  src-gen/ds-signatures/grace-lowered-sig
  trans/semantics/expressions
  trans/semantics/values
  trans/semantics/functions/func-call

signature
  constructors
    BoolV : Bool -> V

  arrows
    bool-call(V, String, List(Exp)) --> V

  native operators
    bool-call-native: String * V * V -> V
    bool-call-native: String * V -> V

rules

  Boolean(True()) --> BoolV(true).

  Boolean(False()) --> BoolV(false).

  bool-call(v, x, []) --> bool-call-native(x, v).
  
  bool-call(v1, x, [v2@BoolV(_)]) --> bool-call-native(x, v1, v2).

  bool-call(BoolV(true), "ifTrue(_)ifFalse(_)", [e1, _]) --> call-qualified(e1, "apply", []).

  bool-call(BoolV(false), "ifTrue(_)ifFalse(_)", [_, e2]) --> call-qualified(e2, "apply", []).


  bool-call(BoolV(true), "ifTrue(_)", [e]) --> call-qualified(e, "apply", []).
  
  bool-call(BoolV(false), "ifTrue(_)", [_]) --> DoneV().
  
  
  bool-call(BoolV(true), "ifFalse(_)", [_]) --> DoneV().  

  bool-call(BoolV(false), "ifFalse(_)", [e]) --> call-qualified(e, "apply", []).
