module trans/semantics/functions

imports
  src-gen/ds-signatures/grace-sig
  trans/semantics/values
  trans/semantics/statements
  trans/semantics/objectmodel
  trans/semantics/types
  
signature
  sorts
    Addr

  constructors
    ClosV: Addr * List(Identifier) * Code * Env -> V
    BlockV : Addr * Addr * List(Identifier) * Code * Env -> V

  arrows
    method-closure(Declaration) --> V
    
    resolve-call(V, String, List(V)) --> V
    call(V, V, List(V)) --> V
    
    read-local(String) --> V
    bind-local(Identifier, V) --> U
    bind-locals(List(Identifier), List(V)) --> U
    
  sort aliases
    Env = Map(String, V)
  
  components
    L : Env
    
  native operators
    nativePrint: V -> V

rules

  MCallL(ID("print(_)"), [e]) --> nativePrint(v)
  where
    e --> v.

  MCallL(ID(x), []) --> read-local(x).
  
  P Exec() |- MCallRecvL(e, ID(x), es) --> resolve-call(v, x, vs)
  where
    e --> v;
    es --> vs.
  
  MQCallOuter(e) --> outer(S)
  where
    e --> RefV(S).

  BlockL(params, _, body) --> BlockV(S, O, params, body, {})
  where
    current-self() --> S;
    current-outer() --> O.
  
rules

  O |- method-closure(MethodL(_, _, _, params, _, code)) --> ClosV(O, params, code, {}).
  
  resolve-call(mv@BlockV(S, _, _, _, _), x, vs) --> v
  where
    str-starts-with(x, "apply") == true;
    call(S, mv, vs) --> v.
  
  resolve-call(recv, x, vs) --> v
  where
    recv =!=> BlockV(_, _, _, _, _);
    lookup-method(recv, x) --> mv;
    call(recv, mv, vs) --> v.
  
  call(S, BlockV(_, O, params, code, env), vs) --> v
  where
    bind-locals(params, vs) :: L env --> _ :: L;
    S, O |- code :: L --> v :: L _.
  
  call(S, ClosV(O, params, code, env), vs) --> v
  where
    bind-locals(params, vs) :: L env --> _ :: L;
    S, O |- code :: L --> v :: L _.
  
  read-local(x) :: L --> L[x] :: L.
  
  bind-local(ID(x), v) :: L --> U() :: L {x |--> v, L}.
  
  bind-local(WildCard(), _) :: L --> U() :: L.
  
  bind-locals([], []) --> U().
  
  bind-locals([x | xs], [v | vs]) --> U()
  where
    bind-local(x, v) --> _;
    bind-locals(xs, vs) --> _.
