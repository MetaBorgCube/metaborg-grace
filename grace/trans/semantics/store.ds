module trans/semantics/store

imports
  trans/semantics/values

signature
  sort aliases
    OHeap = Map(Int, O)
    MHeap = Map(Int, M)
    Locals = Map(String, V)

  variables
    OH : OHeap
    MH : MHeap
    L : Locals

  constructors
    
    getOHeap: OHeap -> V
    getMHeap: MHeap -> V
    
    Stored: O -> V
    WrappedL: Locals -> V
    
    readObject: Ref --> O
    allocateObject: O --> Ref
    writeObject: Ref * O --> O
    
    readMethod: Ref --> M
    allocateMethod: M --> Ref
    writeMethod: Ref * M --> M
    
    writeLocal: String * V --> V
    readLocal: String --> V

rules
  
  readObject(addr) :: OH --> OH[addr].
  
  allocateObject(o) --> addr
  where
    fresh => addr;
    writeObject(addr, o) --> _.

  writeObject(addr, o) :: OH --> o :: OHeap {addr |--> o, OH}.
  
  readMethod(addr) :: MH --> MH[addr].
  
  allocateMethod(m) --> addr
  where
    fresh => addr;
    writeMethod(addr, m) --> _.

  writeMethod(addr, m) :: MH --> m :: MHeap {addr |--> m, MH}.
  
  writeLocal(name, v) :: L --> v :: Locals {name |--> v, L}.
  
  readLocal(name) :: L --> L[name].
