module semantics/trans/lookup

imports
  src-gen/ds-signatures/grace-sig
  src-gen/ds-signatures/grace-lowered-sig

  semantics/trans/values
  semantics/trans/store
  semantics/trans/environment

signature

  constructors
    lookup: String --> M
    lookup-super: String --> M
    lookup-outer: String --> M
    lookup-local: String --> M
    
    get-super: --> Ref
    get-outer: --> Ref

rules
  
  CS CS(rcs), DS DS(rds) |- lookup(name) --> m'
    where
      rcs == rds;
      lookup-super(name) --> m;
      case m of {
        Method(_,_,_) =>
          m => m'
        NoMethod() =>
          lookup-outer(name) --> m'
      }.
  
  CS CS(rcs), DS DS(rds) |- lookup(name) --> m
    where
      rcs != rds;
      lookup-super(name) --> Method(_,_,_);
      DS DS(rcs) |- lookup-super(name) --> m.
  
  CS CS(rcs), DS DS(rds) |- lookup(name) --> m
    where
      rcs != rds;
      lookup-super(name) --> NoMethod();
      lookup-outer(name) --> m.
      
  lookup-outer(name) --> m
    where
      get-outer() --> r;
      CS CS(r), DS DS(r) |- lookup(name) --> m.
  
  lookup-super(name) --> m
  where
    lookup-local(name) --> m;
    case m of {
      Method(_, _, _) =>
        m => m'
      NoMethod() =>
        get-super() --> sr;
        DS DS(sr) |- lookup-super(name) --> m
      NoMethodAndStop() =>
        NoMethod() => m
    }.
  
  dsv |- lookup-local(_) --> NoMethodAndStop()
    where
      readObject(dsv) --> NoObject().
 
  // replace DS(r) with dsv --> rename dsv to ds
  DS DS(r) |- lookup-local(name) --> m
    where
      readObject(r) --> Object(_,_,_);
      member-table(r) --> tbl;
      case tbl[name?] of {
        true =>
          tbl[name] => mr;
          readMethod(mr) --> m
        otherwise =>
          NoMethod() => m
      }.
      
  DS DS(r) |- get-super() --> sup
    where readObject(r) --> Object(_, sup, _).

  DS DS(r) |- get-super() --> r
    where readObject(r) --> NoObject().
    
  DS DS(r) |- get-outer() --> out
    where readObject(r) --> Object(out, _, _).

  DS DS(r) |- get-outer() --> r
    where readObject(r) --> NoObject().
  
  member-table(r) --> tbl
    where
      readObject(r) --> Object(_,_,tbl).
