module trans/semantics/lookup

imports
  src-gen/ds-signatures/grace-sig
  src-gen/ds-signatures/grace-lowered-sig

  trans/semantics/values
  trans/semantics/store
  trans/semantics/context

signature

  constructors
    lookup: String --> M
    lookupSuper: String --> M
    lookupOuter: String --> M
    lookupInOuter: String --> M
    lookupLocal: String --> M
    
    getSuper: --> Ref
    getOuter: --> Ref
    
    memberTable: Ref --> Members

rules
  
  // look in local -> return,
  //   else look both outer and super
  //     if both, error,
  //     else return either
  
  S Self(self), C MethodC() |- lookup(name) :: L --> m
    where
      case L[name?] of {
        true =>
          Method([], [Code(L[name])], self) => m
        otherwise => 
          true == false
      }.
      
  S Self(self) |- lookup(name) --> m
    where
      debug("looking in self slots") => _;
      memberTable(self) --> tbl;
      case tbl[name?] of {
        true => 
          readMethod(tbl[name]) --> m
        otherwise =>          
          debug("no such method: '", name, "' in this object") => _;
          lookupInOuter(name) --> m
      }.
  
  /*
   * Lookup in outer - recursive case (outer is a real object)
   */
  Out Outer(outer) |- lookupInOuter(name) --> m
    where
      debug("looking in outer") => _;
      readObject(outer) --> Object(outerOfOuterRef, _, tbl);
      case tbl[name?] of {
        true => // outer has the name
          readMethod(tbl[name]) --> m
        otherwise => // we have to look in the next outer
          Out Outer(outerOfOuterRef) |- lookupInOuter(name) --> m
      }.
  
  /*
   * Lookup in outer - base case (outer is not an object)
   */
  Out Outer(outer) |- lookupInOuter(name) --> m
    where
      debug("looking in outer") => _;
      readObject(outer) --> NoObject();
      debug("this object has no outer") => _;
      NoMethod(name) => m.
  
  memberTable(r) --> tbl
    where
      readObject(r) --> Object(_,_,tbl).
