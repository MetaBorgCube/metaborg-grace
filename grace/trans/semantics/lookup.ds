module trans/semantics/lookup

imports
  src-gen/ds-signatures/grace-sig
  src-gen/ds-signatures/grace-lowered-sig

  trans/semantics/values
  trans/semantics/store

signature

  constructors
    lookup: String --> M
    lookupSuper: String --> M
    lookupOuter: String --> M
    lookupLocal: String --> M
    
    getSuper: --> Ref
    getOuter: --> Ref
    
    memberTable: Ref --> Members

rules
  
  CS CS(rcs), DS DS(rds) |- lookup(name) --> m'
    where
      rcs == rds;
      lookupSuper(name) --> m;
      case m of {
        Method(_,_,_) =>
          m => m'
        NoMethod() =>
          lookupOuter(name) --> m'
      }.
  
  CS CS(rcs), DS DS(rds) |- lookup(name) --> m
    where
      rcs != rds;
      lookupSuper(name) --> mSuper@Method(_,_,_);
      DS DS(rcs) |- lookupSuper(name) --> mCS;
      case mCS of {
        NoMethod() =>
          mSuper => m
        Method(_,_,_) =>
          mCS => m
      }.
  
  CS CS(rcs), DS DS(rds) |- lookup(name) --> m
    where
      rcs != rds;
      lookupSuper(name) --> NoMethod();
      lookupOuter(name) --> m.
  
  lookupOuter(_) --> NoMethod()
    where
      getOuter() --> r;
      readObject(r) --> NoObject(). 
  
  lookupOuter(name) --> m
    where
      getOuter() --> r;
      CS CS(r), DS DS(r) |- lookup(name) --> m.
  
  lookupSuper(name) --> m
  where
    lookupLocal(name) --> m;
    case m of {
      Method(_, _, _) =>
      NoMethod() =>
        getSuper() --> sr;
        DS DS(sr) |- lookupSuper(name) --> m
      NoMethodAndStop() =>
        NoMethod() => m
    }.
  
  ds |- lookupLocal(_) --> NoMethodAndStop()
    where
      readObject(ds) --> NoObject().
 
  ds |- lookupLocal(name) --> m
    where
      readObject(ds) --> Object(_,_,_);
      memberTable(ds) --> tbl;
      case tbl[name?] of {
        true =>
          tbl[name] => mr;
          readMethod(mr) --> m
        otherwise =>
          NoMethod() => m
      }.
      
  ds |- getSuper() --> sup
    where readObject(ds) --> Object(_, sup, _).

  ds |- getSuper() --> ds
    where readObject(ds) --> NoObject().
    
  ds |- getOuter() --> out
    where readObject(ds) --> Object(out, _, _).

  ds |- getOuter() --> ds
    where readObject(ds) --> NoObject().
  
  memberTable(r) --> tbl
    where
      readObject(r) --> Object(_,_,tbl).
