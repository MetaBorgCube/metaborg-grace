module trans/semantics/lookup

imports
  src-gen/ds-signatures/grace-sig
  src-gen/ds-signatures/grace-lowered-sig

  trans/semantics/values
  trans/semantics/store

signature

  constructors
    lookup: String --> M
    lookup-super: String --> M
    lookup-outer: String --> M
    lookup-local: String --> M
    
    get-super: --> Ref
    get-outer: --> Ref

rules
  
  CS CS(rcs), DS DS(rds) |- lookup(name) --> m'
    where
      rcs == rds;
      lookup-super(name) --> m;
      case m of {
        Method(_,_,_) =>
          m => m'
        NoMethod() =>
          lookup-outer(name) --> m'
      }.
  
  CS CS(rcs), DS DS(rds) |- lookup(name) --> m
    where
      rcs != rds;
      lookup-super(name) --> Method(_,_,_);
      DS DS(rcs) |- lookup-super(name) --> m.
  
  CS CS(rcs), DS DS(rds) |- lookup(name) --> m
    where
      rcs != rds;
      lookup-super(name) --> NoMethod();
      lookup-outer(name) --> m.
      
  lookup-outer(name) --> m
    where
      get-outer() --> r;
      CS CS(r), DS DS(r) |- lookup(name) --> m.
  
  lookup-super(name) --> m
  where
    lookup-local(name) --> m;
    case m of {
      Method(_, _, _) =>
        m => m'
      NoMethod() =>
        get-super() --> sr;
        DS DS(sr) |- lookup-super(name) --> m
      NoMethodAndStop() =>
        NoMethod() => m
    }.
  
  ds |- lookup-local(_) --> NoMethodAndStop()
    where
      readObject(ds) --> NoObject().
 
  ds |- lookup-local(name) --> m
    where
      readObject(ds) --> Object(_,_,_);
      member-table(ds) --> tbl;
      case tbl[name?] of {
        true =>
          tbl[name] => mr;
          readMethod(mr) --> m
        otherwise =>
          NoMethod() => m
      }.
      
  ds |- get-super() --> sup
    where readObject(ds) --> Object(_, sup, _).

  ds |- get-super() --> ds
    where readObject(ds) --> NoObject().
    
  ds |- get-outer() --> out
    where readObject(ds) --> Object(out, _, _).

  ds |- get-outer() --> ds
    where readObject(ds) --> NoObject().
  
  member-table(r) --> tbl
    where
      readObject(r) --> Object(_,_,tbl).
