module trans/semantics/objects/path

signature
  sorts
    PathPart
  
  sort aliases
    ResPath = List(PathPart) // invariant: empty means no path found
    
  constructors
    PL: String -> PathPart // local
    PP: Int * String -> PathPart // name S in parent I
    PO: PathPart // outer
  
  arrows
    path-found(ResPath) --> Bool
    path-local(ResPath) --> Bool
    path-inherited(ResPath) --> Bool
    path-strictly-inherited(ResPath) --> Bool
    
    path-lexical(ResPath) --> Bool
    path-strictly-lexical(ResPath) --> Bool
    
    path-outers-only(ResPath) --> Bool    
    path-parents-only(ResPath) --> Bool
    
    path-aliased(String, ResPath) --> Bool
    
    path-local-aliased(String, ResPath) --> Bool
rules

  path-found([]) --> false
  
  path-found([_|_]) --> true
  
  
  path-local([PL(_)]) --> true
  
  path-local([p | _]) --> false
  where
    p =!=> PL(_)
  
  
  path-inherited([PP(_, _)| _]) --> true
  
  path-inherited([p | _]) --> false
  where
    p =!=> PP(_, _)

  path-strictly-inherited(path) --> path-parents-only(`path')
  where
    `path => [PL(_) | path']
  
  path-lexical([PO()|_]) --> true
  
  path-lexical([p | _]) --> false
  where
    p =!=> PO()
    
  
  path-strictly-lexical(paths) --> path-outers-only(`paths')
  where
    `paths => [PL(_) | paths']
  
  
  path-outers-only([]) --> true
  
  path-outers-only([PO() | path]) --> path-outers-only(path)
  
  path-outers-only([p | _]) --> false
  where
    p =!=> PO()
  
  
  path-parents-only([]) --> true
  
  path-parents-only([PP(_, _) | path]) --> path-parents-only(path)
  
  path-parents-only([p | _]) --> false
  where
    p =!=> PP(_, _)
  
  path-aliased(x1, [PL(x2)]) --> true
  where
    x1 != x2
  
  path-aliased(x1, [PL(x2)]) --> false
  where
    x1 == x2
  
  path-aliased(x1, [p | path]) --> path-aliased(x1, path)
  where
    p =!=> PL(_)
  
  path-local-aliased(x1, [PP(_, x2) | _]) --> true
  where
    x1 != x2
  
  path-local-aliased(x1, [PP(_, x2) | _]) --> false
  where
    x1 == x2
  
  path-local-aliased(_, [p | _]) --> false
  where
    p =!=> PP(_, _)
  