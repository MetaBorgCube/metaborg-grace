module trans/semantics/objects/obj-constr

imports
  trans/semantics/objects/obj-desugar
  trans/semantics/objects/obj-repr
  trans/semantics/objects/obj-init
  trans/semantics/store
  
signature
  sorts
    EvalMode
  
  constructors
    E: EvalMode // regular evaluation mode
    B: EvalMode // object building mode
  
  components
    EB : EvalMode
  
  arrows
    construct-obj(Exp) --> Addr
    install-parents(List(LInherit)) --> Parents
    install-parent(LInherit) --> Parent
    install-slots(LSlots) --> Slots
    install-methods(LMethods) --> Methods
    install-methods-aux(List(String), LMethods) --> Methods
    
rules

  o@ObjectL(_, _, _) --> desugar-obj(o).
  
  S, EB |- o@LObj(_, _, _) --> RefV(S')
  where
    construct-obj(o) --> S';
    case EB of {
      E() =>
        S S', O S |- init-obj(S') --> _
      otherwise =>
    }.

    
  EB B() |- MCallRecvL(e, ID(x), es) --> v
  where
    EB E() |- e --> recv;
    EB E() |- es --> vs;
    EB B() |- call-qualified(recv, x, vs) --> v.
  
  S |- construct-obj(LObj(parents, slots, methods)) --> S'
  where
    allocate(Obj(S, [], {}, {})) --> S';
    O S |- install-parents(parents) --> parent-links;
    O S |- install-slots(slots) --> slots-table;
    O S |- install-methods(methods) --> methods-table;
    log("Constr: " ++ int2string(S') ++ " methods=" ++ str(allkeys(methods) : AST)) --> _;
    update(S', Obj(S, `parent-links, slots-table, methods-table)) --> _.

  install-parents([]) --> [].
  
  install-parents([p | parents]) --> [p' | parents']
  where
    install-parent(p) --> p';
    install-parents(parents) --> parents'.
  
  install-parent(LInherit(e, alias-map, exclude-map)) --> (S, alias-map, exclude-map)
  where
    EB B() |- e --> RefV(S).
  
  
  install-slots([]) --> {}.
  
  install-slots([s | ss]) --> { s |--> UninitializedV(), slots}
  where
    install-slots(ss) --> slots.
  
  install-methods(methods) --> install-methods-aux(method-names, methods)
  where
    allkeys(methods) => method-names.
  
  install-methods-aux([], _) --> {}.
  
  install-methods-aux([m | ms], methods) --> {m |--> clos, methods-map}
  where
    method-closure(methods[m]) --> clos;
    install-methods-aux(ms, methods) --> methods-map.

