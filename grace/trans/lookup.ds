module trans/lookup

imports
  src-gen/ds-signatures/grace-sig
  src-gen/ds-signatures/grace-lowered-sig

  trans/values
  trans/store
  trans/environment

signature

  constructors
    lookup: String --> M
    lookup-super: String --> M
    lookup-outer: String --> M
    lookup-local: String --> M
    
    get-super: Ref --> Ref
    get-outer: Ref --> Ref

rules
  
  CS CS(rcs), DS DS(rds) |- lookup(name) --> m'
    where
      rcs == rds;
      lookup-super(name) --> m;
      case m of {
        Method(_,_,_) =>
          m => m'
        NoMethod() =>
          lookup-outer(name) --> m'
      }.
  
  CS CS(rcs), DS DS(rds) |- lookup(name) --> m
    where
      rcs != rds;
      lookup-super(name) --> Method(_,_,_);
      DS DS(rcs) |- lookup-super(name) --> m.
  
  CS CS(rcs), DS DS(rds) |- lookup(name) --> m
    where
      rcs != rds;
      lookup-super(name) --> NoMethod();
      lookup-outer(name) --> m.
      
  lookup-outer(name) --> m
    where
      get-outer(0) --> r;
      CS CS(r), DS DS(r) |- lookup(name) --> m.
  
  lookup-super(name) --> m
  where
    lookup-local(name) --> m;
    case m of {
      Method(_, _, _) =>
        m => m'
      NoMethod() =>
        get-super(0) --> sr;
        DS DS(sr) |- lookup-super(name) --> m
      NoMethodAndStop() =>
        NoMethod() => m
    }.
  
  DS DS(r) |- lookup-local(_) --> NoMethodAndStop()
    where
      readObject(r) --> NoObject().
 
  DS DS(r) |- lookup-local(name) --> m
    where
      readObject(r) --> Object(_,_,_);
      member-table(r) --> tbl;
      case tbl[name?] of {
        true =>
          tbl[name] => mr;
          readMethod(mr) --> m
        otherwise =>
          NoMethod() => m
      }.
      
  DS DS(r) |- get-super(_) --> sup
    where readObject(r) --> Object(_, sup, _).

  DS DS(r) |- get-super(_) --> r
    where readObject(r) --> NoObject().
    
  DS DS(r) |- get-outer(_) --> out
    where readObject(r) --> Object(out, _, _).

  DS DS(r) |- get-outer(_) --> r
    where readObject(r) --> NoObject().
  
  member-table(r) --> tbl
    where
      readObject(r) --> Object(_,_,tbl).
