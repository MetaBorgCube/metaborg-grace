module trans/desugar/lower

imports

    libstratego-lib
    libstratego-gpp
    libstratego-aterm
    src-gen/signatures/grace-sig
    src-gen/signatures/grace-lowered-sig
    src-gen/signatures/General-sig
    
    trans/desugar/desugar-common
    
    trans/desugar/lower-booleans
    
    
    
rules

  lower-all = lower
  
  lower = 
          lower-boolean-to-object <+
          lower-mdecl <+
          lower-mcallop <+
          lower-mcallimpl <+
          lower-mcallcanonical <+
          lower-fail
  
  lower-mcallop:
    MCallOp(recv, name, argtype, args) ->
      MCallCanonicalL(recv, <name-to-string> name, argtype, <lower-arguments> args)
  
  lower-mcallimpl: 
    MCallImpl([Part(ID(name),NoArgType(),args)]) -> MCallImplL(name,NoArgType(), aList)
      where
        aList := <flatten-list> <lower-arguments> args
  
  lower-mdecl:
    MethodDecl(prfx, [MethodID(mName, argType, annotations, ps)], methodType, MethodBody(cs), wherec) ->
      MethodDeclL(prfx,name,argType,annotations, paramNames, paramTypes, methodType, cs, wherec)
      with
        (paramNames, paramTypes) := <lower-params> ps;
        name := <name-to-string> mName 

  lower-mdecl:
    MethodDecl(prfx, [MethodOp(mName, argType, annotations, ps)], methodType, MethodBody(cs), wherec) ->
      MethodDeclL(prfx,name,argType,annotations, paramNames, paramTypes, methodType, cs, wherec)
      with
        (paramNames, paramTypes) := <lower-params> ps;
        name := <name-to-string> mName 
  
  lower-mcallcanonical: 
    MCallCanonical(_, recv, ID(name), argType, args) ->
      MCallCanonicalL(recv, name, argType, <lower-arguments> args)
  
  lower-params: NoParams() -> ([], [])
  lower-params: Params(ps) -> <lower-params> ps
  lower-params: [Param(n, typeAnn)] -> ([n], [typeAnn])
  lower-params: [Param(n, typeAnn) | bs] -> ([n | ns ], [typeAnn | tas])
    where
      (ns, tas) := <lower-params> bs
  
  lower-arguments: ArgNumber(a) -> [Number(a)]
  lower-arguments: ArgString(a) -> [String(a)]
  lower-arguments: ArgBoolean(a) -> [Boolean(a)]
  lower-arguments: NoArgs() -> []
  lower-arguments: ArgsParen(as) -> as
  lower-arguments: a -> a
  
  
  lower-fail: a -> <fail>